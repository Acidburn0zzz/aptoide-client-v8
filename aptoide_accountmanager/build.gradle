/*
 * Copyright (c) 2016.
 * Modified by SithEngineer on 08/07/2016.
 */

apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
retrolambda {
    jvmArgs '-noverify'
    //defaultMethods true
}

Properties localProperties = new Properties()
localProperties.load(new FileInputStream(project.file("../../aptoideProperties/local.properties")))

def gmsServerId = localProperties.GMS_SERVER_ID

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    def GMS_CONFIGURED = "GMS_CONFIGURED"
    def GMS_SERVER_ID = "GMS_SERVER_ID"
    def TRUE = "true"
    def FALSE = "false"
    def BOOLEAN = "boolean"
    def STRING = "String"
    def FACEBOOK_CONFIGURED = "FACEBOOK_CONFIGURED"

    defaultConfig {
        minSdkVersion 15

        buildConfigField BOOLEAN, GMS_CONFIGURED, (gmsServerId != null).toString()
        buildConfigField STRING, GMS_SERVER_ID, "\"" + gmsServerId.toString() + "\""

        def facebookAppId = localProperties.FACEBOOK_APP_ID
        def facebookAppIdExists = facebookAppId != null
        buildConfigField BOOLEAN, FACEBOOK_CONFIGURED, facebookAppIdExists.toString()
        facebookAppId = facebookAppIdExists ? facebookAppId.toString() : "0"
        resValue "string", "facebook_app_id", "\"" + facebookAppId + "\""

        consumerProguardFiles "proguard-rules.txt"
    }

    lintOptions {
        abortOnError false
    }

    publishNonDefault true

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
        }
        debug {
            minifyEnabled false
            shrinkResources false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        incremental true
    }
}

dependencies {

    //
    // inter-projects dependencies
    //

    debugCompile project(path: ':aptoidepreferences', configuration: 'debug')
    releaseCompile project(path: ':aptoidepreferences', configuration: 'release')

    debugCompile project(path: ':networkclient', configuration: 'debug')
    releaseCompile project(path: ':networkclient', configuration: 'release')

    debugCompile project(path: ':utils', configuration: 'debug')
    releaseCompile project(path: ':utils', configuration: 'release')

    //
    // custom libs in folders
    //

    compile fileTree(dir: 'libs', include: ['*.jar'])

    //
    // from remote repos
    //

    testCompile "junit:junit:${JUNIT_VERSION}"

    //noinspection GradleDependency
    compile "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"
    //noinspection GradleDependency
    compile "com.android.support:design:${SUPPORT_LIB_VERSION}"

    //noinspection GradleDependency
    compile "com.google.android.gms:play-services-basement:${PLAY_SERVICES_VERSION}"
    //noinspection GradleDependency
    compile "com.google.android.gms:play-services-auth:${PLAY_SERVICES_VERSION}"

    compile "com.facebook.android:facebook-android-sdk:${FACEBOOK_ANDROID_SDK_VERSION}"

    compile "io.reactivex:rxjava:${RXJAVA_VERSION}"
    compile "io.reactivex:rxandroid:${RXANDROID_VERSION}"
    // And ProGuard rules for RxJava!
    compile "com.artemzin.rxjava:proguard-rules:${RXJAVA_PROGUARD_RULES_VERSION}"

    provided "org.projectlombok:lombok:${LOMBOK_VERSION}"
    // Just for lombok not to complain
    provided "org.glassfish:javax.annotation:${JAVAX_ANNOTATIONS_VERSION}"
}
